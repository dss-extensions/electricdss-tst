Clear 
// EPSL impedance is 10% on 75 kVA positive sequence, 5% zero sequence, assume X/R = 2
new circuit.HWPLL3 
~ basekv=4.16 pu=1.0 angle=0 phases=3 bus1=SourceBus r1=0.029 x1=0.058 r0=0.014 x0=0.029

New XYcurve.z_pll npts=3
~ xarray=[ 1.0000 -1.98515  0.98531] // denominator
~ yarray=[ 0.0000  0.01485 -0.01469] // numerator

New vccs.pv Phases=3 Bus1=SourceBus Prated=375e3 Vrated=4160 Ppct=100 
~ filter='z_pll' fsample=10000 rmsmode=true imaxpu=1.15
~ vrmstau=0.01 irmstau=0.05

new fault.flt bus1=Sourcebus.1 phases=1 r=0.001 temporary=yes ontime=0.1 //ontime=0.0821 // ontime=0.0768

new monitor.invvi element=vccs.pv terminal=1 mode=0
new monitor.invpq element=vccs.pv terminal=1 mode=1
new monitor.invst element=vccs.pv terminal=1 mode=3
new monitor.fltvi element=fault.flt terminal=1 mode=0

Set Voltagebases=[4.16]
set maxiterations=100
calcv

set mode=snap
solve 

set mode=dynamic
set stepsize=0.001 // 0.0001 matches fsample
set number=250     // 2500 matches fsample
Solve

plot type=monitor obj=invvi channels=[1 3 5 7 9 11] bases=[2400 2400 2400 52.083 52.083 52.083]
plot type=monitor obj=invpq channels=[1 3 5] bases=[125 125 125]
plot type=monitor obj=invst channels=[1 2 3 4]

//export monitors invvi
//export monitors invpq
//export monitors invst
//export monitors fltvi

