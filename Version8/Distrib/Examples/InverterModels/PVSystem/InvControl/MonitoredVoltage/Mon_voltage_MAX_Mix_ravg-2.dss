Clear

New Circuit.TheveninEquivalente bus1=A pu=1.0 basekv=13.8 
~ Z0=[10, 10] Z1=[10, 10]

New Wiredata.CondutorFase GMR=0.0244 DIAM=0.721 RAC=0.306
~ normamps=530
~ Runits=mi RADunits=in GMRunits=ft

New Wiredata.CondutorNeutro GMR=0.00814 DIAM=0.563 RAC=0.592
~ normamps=340
~ Runits=mi RADunits=in GMRunits=ft

New Linegeometry.Poste nconds=3 nphases=3 reduce=No
~ cond=1 wire=CondutorFase     x= -4     h=29   units=ft
~ cond=2 wire=CondutorFase     x= -1.5   h=29   units=ft
~ cond=3 wire=CondutorFase     x= 3      h=29   units=ft

New Line.LineAB bus1=A bus2=B
~ geometry=Poste
~ length=10 units=km
~ earthmodel=Carson

New Transformer.Tr  phases=3 xhl=0.0001  
~ wdg=1 bus=B kV=13.8 kVA=2400 conn=wye
~ wdg=2 bus=C kV=0.48 kVA=2400 conn=wye

New XYCurve.Eff npts=4 xarray=[.1 .2 .4 1.0] yarray=[1 1 1 1]

New XYCurve.FatorPvsT npts=4 xarray=[0 25 75 100] yarray=[1 1 1 1] 

New Loadshape.Irrad npts=20 sinterval=1 csvfile=PV_irrad.csv

New Tshape.Temp npts=24 interval=1 
~ temp=[25 25 25 25 25 25 25 25 35 40 45 50 60 60 55 40 35 30 25 25 25 25 25 25]

New PVSystem.PV phases=3 bus1=C Pmpp=1000 kV=0.48 kVA=1200 conn=wye EffCurve=Eff 
~ P-TCurve=FatorPvsT %Pmpp=100 irradiance=1 daily=Irrad Tdaily=Temp   

New XYcurve.generic npts=5 yarray=[1 1 0 -1 -1] xarray=[0.5 0.92 1.0 1.05 1.5]

New InvControl.Voltvar Mode=voltvar voltage_curvex_ref=ravg avgwindowlen=2s 
~ vvc_curve1=generic deltaQ_factor=0.2 RefReactivePower=VARMAX varchangetolerance=0.0001
~ monVoltageCalc=MAX MonBus=[A.1.2 A.2 C.1.2 C.2] monBusesVbase=[13800 7967.433415 480 277.128129]

New Monitor.PV_currents element=PVSystem.PV terminal=1 mode=0
New Monitor.PV_powers element=PVSystem.PV terminal=1 mode=1 ppolar=no
New Monitor.PV_s element=PVSystem.PV terminal=1 mode=1 ppolar=yes
New Monitor.PV_v element=PVSystem.PV terminal=1 mode=3

Set voltagebases=[13.8 0.48]
Calcvoltagebases

Set Maxcontroliter = 1000
Set mode=daily
Set stepsize=1s

Set number=8
set casename=Mon_voltage_MAX_Mix_ravg_sec8
Solve
Show Voltage LN Nodes

Set number=1
set casename=Mon_voltage_MAX_Mix_ravg_sec9
Solve
Show Voltage LN Nodes

Set number=1
set casename=Mon_voltage_MAX_Mix_ravg_sec10
Solve
Show Voltage LN Nodes
Show Power kVA elements


Set number=10
set casename=Mon_voltage_MAX_Mix_ravg
Solve

Export monitors pv_powers
Export monitors pv_v
Export monitors pv_currents
