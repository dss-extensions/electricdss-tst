Clear

// Example of 59N relay for generator protection
// Uses Y:broken Delta PT at the generator location with one corner grounded
// Voltage relay connected across open point to ground

New Circuit.59Nrelay BasekV=0.48
New line.Line1 Bus1=sourcebus  Bus2=Genbus
New Generator.gen1 bus1=genbus kV=0.48 kW=100 

! Pattern for 1-phase PTs  (1:1 ratio)
New Xfmrcode.PT phases=1 windings=2 kvs=[.48 .48] kVAs=[10 10] XHL=10 conns=[wye wye]

! one-point TCC curve for 59 inst operation at 0.1 s
New TCC_Curve.3V0  Npts=1 c_array=[.3] T_array=[.1]   ! trip at 3V0=0.3 pu voltage for OV

! broken delta PT 
New Transformer.PT1 xfmrcode=PT Buses=[Genbus.1.0 Delta.1.0]
New Transformer.PT2 xfmrcode=PT Buses=[Genbus.2.0 Delta.2.1]
New Transformer.PT3 xfmrcode=PT Buses=[Genbus.3.0 Delta.3.2  ! 3 to 0 is broken delta voltage
!    Note polarity of secondary of PT3

! Put relay on  PT3 terminal 2 (Delta.3.2) and trip the generator when voltage exceeds pickup value

New "Relay.mfrov/uv" MonitoredObj=Transformer.PT3 MonitoredTerm=2 SwitchedObj=Line.line1 SwitchedTerm=1 type=voltage 
~  kvbase=0.277 Shots=1 Delay=0 Overvoltcurve=3V0 RecloseIntervals=(5.0)

Set Voltagebases=[0.48]
Calcv
Solve
Show v ln nodes

New monitor.m1 Transformer.PT3 2  ! secondary side of PT3
New Fault.F1 Bus1=Genbus  ON=0.3
solve mode=dynamics stepsize=0.1 number=10  ! solve 1.0 s
show mon m1

