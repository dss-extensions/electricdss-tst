!Secondary Low-Voltage Circuit Modelsâ€”How Good is Good Enough?
!IEEE Transactions on Industry Applications
Clear

New circuit.equiv_stiff MVAsc1=100000000 MVAsc3=100000000 basekv=7.2 bus1=XfmrMV angle=-30

! 1-phase equivalent of the split-phase transformer with percent short-circuit impedance: r+jx=1.18+j1.44
New Transformer.ServiceXmfr phases=1 windings=2 Xhl=1.44 %Rs=(0.59 0.59) ppm_antifloat=0  ! %imag = 0.5 %NoLoadLoss = 0.2
~ wdg=1 bus=XfmrMV.1.2 conn=delta kV=7.2 kVA=25
~ wdg=2 bus=XfmrLV.1.0 conn=wye kV=0.24 kVA=25

! Linecodes for the 1-phase equivalents of the triplex cables
New Linecode.Triplex1PhEquivalent4   nphases=1 units=kft rmatrix=[0.9290] xmatrix=[0.0891] cmatrix=[0] normamps=150 emergamps=195 Rg=0 Xg=0 
New Linecode.Triplex1PhEquivalent1/0 nphases=1 units=kft rmatrix=[0.3674] xmatrix=[0.0856] cmatrix=[0] normamps=280 emergamps=350 Rg=0 Xg=0 
New Linecode.Triplex1PhEquivalent3/0 nphases=1 units=kft rmatrix=[0.2314] xmatrix=[0.0873] cmatrix=[0] normamps=375 emergamps=480 Rg=0 Xg=0 

! The 1-phase equivalents of the triplex cables
New Line.Triplex1 bus1=XfmrLV.1.0 bus2=BusLoad1.1.0 linecode=Triplex1PhEquivalent4 length=48 units=ft 
New Line.Triplex2 bus1=XfmrLV.1.0 bus2=BusLoad2.1.0 linecode=Triplex1PhEquivalent3/0 length=167 units=ft

! Loads without loadshapes
New Load.Load1 kV=0.24 phases=1 bus1=BusLoad1.1.0 model=1 conn=wye kW=10 kvar=2 Vminpu=0.6 Vmaxpu=2.0
New Load.Load2 kV=0.24 phases=1 bus1=BusLoad2.1.0 model=1 conn=wye kW=12 kvar=4 Vminpu=0.6 Vmaxpu=2.0

Set voltagebases=[7.2 0.41569219 0.240]
Calcvoltagebases

setbusxy bus=XfmrLV x=0 y=0
setbusxy bus=BusLoad1 x=1 y=1
setbusxy bus=BusLoad2 x=1 y=-1

New EnergyMeter.sub element=Transformer.ServiceXmfr terminal=1 

set mode=snapshot
solve

!plot circuit Power max=20 n n C1=$00FF0000
!plot circuit Voltage Max=2000 dots=n labels=n subs=y C1=$00FF0000
!plot circuit Losses Max=5 dots=n labels=n subs=n C1=$00FF0000
!plot circuit Current Max=2000 dots=n labels=n subs=n C1=$00FF0000
!plot circuit Max=20 dots=n labels=n subs=n C1=$00FF0000 thickness=80

!Show Currents Elements
!Show Voltage LN Nodes
!Show Powers kva Elements