! Base Circuit with a Single Storage 
Clear

New Circuit.Source bus1=A basekv=13.8 phases=3 pu=1 Z0=[10, 10] Z1=[10, 10]

! Inverter Efficiency Curve
New XYCurve.Eff npts=4 xarray=[.1  .2  .4  1.0]  yarray=[.86  .9  .93  .97] 

New Storage.A phases=3 bus1=A kv=13.8
~ %charge=100 %discharge=100 %reserve=20
~ %effcharge=90 %effdischarge=90
~ %idlingkW=2 
~ kWhrated= 10000 %stored=80 state=idling
~ dispmode=default model=1 kVA=1000 kWrated=900 kvarMax=800 %kWRated=100  
~ EffCurve=Eff %cutin=5 %cutout=5 varfollowinverter=false wattpriority=True
~ %PminkvarMax = 20 %PminNoVars = 10
~ vminpu = 0.8 vmaxpu= 1.2
 
New Monitor.Mon_StorageA_State element=Storage.A mode=3
New Monitor.Mon_StorageA_V element=Storage.A mode=0

Set voltagebases = [13.8]
Calcvoltagebases

! kVA Violation Only
New LoadShape.dispatchFollow interval=1 npts=24 mult = [0.0, -0.01, -0.08, -0.12, -0.16, -0.30, -0.50, -0.96, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.01, 0.08, 0.12, 0.16, 0.30, 0.50, 0.96, 0.0, 0.0]

Edit Storage.A dispmode=follow daily=dispatchFollow pf=-0.8

! P Priority
Set casename=Ppriority
Edit Storage.A wattpriority=true

! Q Priority
!Set casename=Qpriority
!Edit Storage.A wattpriority=false

! PF Priority
!Set casename=PFpriority
!Edit Storage.A PFpriority=true

Set mode=Daily
Set maxcontroliter=50
Set stepsize=1h

Solve

Export monitors Mon_StorageA_State
Export monitors Mon_StorageA_V 