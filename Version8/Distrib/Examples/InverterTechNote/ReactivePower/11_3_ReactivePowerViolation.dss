! Base Circuit with a Single Storage 
Clear

New Circuit.Source bus1=A basekv=13.8 phases=3 pu=1 Z0=[10, 10] Z1=[10, 10]

! Inverter Efficiency Curve
New XYCurve.Eff npts=4 xarray=[.1  .2  .4  1.0]  yarray=[.86  .9  .93  .97] 

New Storage.A phases=3 bus1=A kv=13.8
~ %charge=100 %discharge=100 %reserve=20
~ %effcharge=90 %effdischarge=90
~ %idlingkW=2 
~ kWhrated= 10000 %stored=80 state=idling
~ dispmode=default model=1 kVA=1000 kWrated=900 kvarMax=800 %kWRated=100  
~ EffCurve=Eff %cutin=5 %cutout=5 varfollowinverter=false wattpriority=True
~ %PminkvarMax = 20 %PminNoVars = 10
~ vminpu = 0.8 vmaxpu= 1.2
 
New Monitor.Mon_StorageA_State element=Storage.A mode=3
New Monitor.Mon_StorageA_V element=Storage.A mode=0

Set voltagebases = [13.8]
Calcvoltagebases

! Reactive Power Violation (linear region, kvarMax, kVA)
New LoadShape.dispatchFollow interval=(1 3600 /) npts=24 mult = [0, -0.01, -0.08, -0.12, -0.14, -0.16, -0.30, -0.50, -0.6, -0.8, -0.95, 0, 0.01, 0.08, 0.12, 0.14, 0.16, 0.30, 0.50, 0.6, 0.8, 0.95, 0, 0]

Edit Storage.A dispmode=follow daily=dispatchFollow %cutin=0 %cutout=0 %PminNoVars=0 kvarMaxAbs=600 %idlingkW=0

! VV Curve
New XYCurve.vv_curve npts=5 yarray=[1 1 0 -1 -1] xarray=[0.5 0.92 1.0 1.08 1.5]  

New InvControl.InvCtrl combiMode=VV_DRC dbVMin=1 dbVMax=1 arGraLowV=50  
~ arGraHiV=50 dynReacAvgWindowLen=2s refReactivePower=VARMAX vvc_curve1=vv_curve 
~ varChangeTolerance=0.001 

! P Priority
Set casename=Ppriority
Edit Storage.A wattpriority=true

! Q Priority
!Set casename=Qpriority
!Edit Storage.A wattpriority=false

Set mode=Daily
Set maxcontroliter=50
Set stepsize=1s

! 1 - 3am 
Edit VSource.source pu=1.0
Set number=3
Solve

! 4am 
Edit VSource.source pu=0.98
Set number=1
Solve

! 5am  
Edit VSource.source pu=0.96
Solve

! 6am 
Edit VSource.source pu=0.94
Solve

! 7am 
Edit VSource.source pu=0.92
Solve

! 8am 
Edit VSource.source pu=1.05
Solve

! 9 - 10am 
Edit VSource.source pu=1.00
Set number=2
Solve

! 11am  
Edit VSource.source pu=0.97
Set number=1
Solve

! 12pm - 3pm  
Edit VSource.source pu=1.00
Set number=4
Solve

! 4pm  
Edit VSource.source pu=1.03
Set number=1
Solve

! 5pm
Edit VSource.source pu=1.06
Solve

! 6pm 
Edit VSource.source pu=1.1
Solve

! 7 - 9pm 
Edit VSource.source pu=0.98
set number=3
Solve

! 10pm  
Edit VSource.source pu=1.16
set number=1
Solve

! 11pm - 12am
Edit VSource.source pu=1.0
Set number=2
Solve

Export monitors Mon_StorageA_State
Export monitors Mon_StorageA_V 
Export eventlog